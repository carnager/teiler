# Maintainer: Rasmus Steinke <rasi at xssn dot at>
# Contributor: Christian Rebischke <Chris.Rebischke@gmail.com>

pkgname=teiler-git
pkgver=193.1c0eb02
pkgrel=1
pkgdesc="a simple screenshot/screencast tool written in bash"
arch=('any')
url='http://teiler.53280.de'
license=('GPL')
depends=('bc' 'ffmpeg' 'scrot' 'xclip' 'dzen2-xft-xpm-xinerama-fixed-svn' 'pygtk' 'xininfo-git' 'xdotool' 'xorg-xwininfo')
optdepends=('fb-client: 		Upload to paste.xinu.at (or selfhosted filebin)'
            'openssh: 			Upload to SSH server'
            'dmenu2: 			Backend for teiler'
            'simpleswitcher-dd-git: 	Alternative backend for teiler'
            'xorg-xwininfo: 		Screencast single windows'
            'gtk2:                      Image to Clipboard'
            'gtk3:                      Image to Clipboard'
            'xdotool: 			Screencast single windows'
            'imgur: Upload to imgur.com'
            'ix: Upload to ix.io')

options=(!strip)
install="teiler.install"
makedepends=('git')
source=('git+http://git.53280.de/carnager/teiler')

pkgver() {
	cd teiler
	printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build () {
  cd ${srcdir}/teiler/imgclip
  make
}  

package() {

  install -D -m755 "${srcdir}/teiler/teiler" \
    "${pkgdir}/usr/bin/teiler"

  install -D -m644 "${srcdir}/teiler/config.example" \
    "${pkgdir}/usr/share/doc/teiler/config.example"

  install -D -m644 "${srcdir}/teiler/dmenurc" \
    "${pkgdir}/usr/share/doc/teiler/dmenurc"

  cd ${srcdir}/teiler/imgclip
  make DESTDIR="$pkgdir" install
}

md5sums=('SKIP')
